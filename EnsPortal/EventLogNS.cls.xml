<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="EnsPortal.EventLogNS">
<Description>
This is the Event Log page of the ZEN Ensemble application.
With namespace support</Description>
<IncludeCode>Ensemble</IncludeCode>
<Super>EnsPortal.EventLog</Super>
<System>4</System>
<TimeCreated>60310,50851.071342</TimeCreated>

<Parameter name="BaseSQL">
<Type>%String</Type>
<Default>SELECT Type,ID,TimeLogged,SessionId,Job,ConfigName,Text,TraceCat,SourceClass,SourceMethod,$ZNSPACE As Namespace FROM Ens_Util.Log</Default>
</Parameter>

<XData name="resultsPane">
<Description>
This pane displays event log activity table. User may click on a row and details are displayed to the side.</Description>
<XMLNamespace>http://www.intersystems.com/zen</XMLNamespace>
<Data><![CDATA[
<pane>
<vgroup valign="top">
<!--<spacer height="5"/>-->
<tablePane id="table" 
	cellSpacing="2"
	dataSource="columns"
	tableName="Ens_Util.Log"
	useSnapshot="false"
	showZebra="true"
	valueColumn="ID"
	maxRows="1000"
	useKeys="true"
	showQuery="false"
	onselectrow="zenPage.selectItem(zenThis);"
	nowrap="true" 
	onmouseoverClass="tpMouseOver"
	enableToggleSelect="true"
	onupdate="setTimeout('zenPage.onAfterSearch();',50);"
	initialExecute="false"
	OnCreateResultSet="CheckConditions"
	unlockSession="true"
	>
<!-- provide header values for all columns so they can be localized -->
<column header="Type" colName="Type" style="text-align:left;" width="65" OnDrawCell="DrawLocalType"
        title="Alert, Assert, Error, Info, Trace, or Warning"/>
<column header="ID" colName="ID" style="text-align:left;" width="90"
		title="Log Event ID"/>
<column header="Namespace" colName="Namespace" style="text-align:left;" width="90"
		title="Namespace"/>
<column header="Time Logged" colName="TimeLogged999" id="idTimeLogged999" width="180" colExpression="{fn RIGHT(%EXTERNAL(TimeLogged),999)}"
		title="Time the event was noted in the log" hidden="true" />
<column header="Time Logged" colName="TimeLogged12" id="idTimeLogged12" width="180" colExpression="{fn RIGHT(%EXTERNAL(TimeLogged),12)}"
		title="Time the event was noted in the log" />
<column header="Session" colName="SessionId" width="90"
       title="Identifier for the session in which the event occurred"
       link=" "
        OnDrawCell="DrawSessionCell" />
<column header="Job" colName="Job" width="90"
        title="System job in which the event occurred"
        OnDrawCell="DrawMethodCell" />
<column header="Source" colName="ConfigName" width="200"
        title="Ensemble configuration name of the item that triggered the event"
        OnDrawCell="DrawMethodCell" />
<column header="Text" colName="Text" width="1200"
        title="Descriptive text logged with the event"
        OnDrawCell="DrawTextCell" />
<column colName="TraceCat" hidden="true"/>
<column colName="SourceClass" hidden="true"/>
<column colName="SourceMethod" hidden="true"/>
<!-- we will not be switching colors by user preference; we will always use zebra plus 1-column color coding -->
<condition id="condAlert" colName="Type" predicate="EQ" value="Alert" targetCol="Type" cellStyle="background: yellow; color: darkred; font-weight: bold;" />
<condition id="condError" colName="Type" predicate="EQ" value="Error" targetCol="Type" cellStyle="background: pink; color: darkred; font-weight: bold;" />
<condition id="condWarning" colName="Type" predicate="EQ" value="Warning" targetCol="Type" cellStyle="background: orange; color: darkred; font-weight: bold;" />
<condition id="condAssert" colName="Type" predicate="EQ" value="Assert" targetCol="Type" cellStyle="background: silver; color: darkred; font-weight: bold;" />
<condition id="condTrace" colName="Type" predicate="EQ" value="Trace" targetCol="Type" cellStyle="background: lightblue; color: darkblue; font-weight: bold;" />
<!-- it does not work to have a condition Type EQ Info and also have Method EQ Start/StopProduction so no condition on Type EQ Info -->
<condition id="condStart" colName="SourceMethod" predicate="EQ" value="StartProduction" targetCol="Type" cellStyle="background: lightgreen; color: darkgreen; font-weight: bold;" />
<condition id="condStop" colName="SourceMethod" predicate="EQ" value="StopProduction" targetCol="Type" cellStyle="background: lightgreen; color: darkgreen; font-weight: bold;" />
</tablePane>
<!-- set default auto refresh to "None" -->
<timer id="timer" ontimeout="zenPage.timeout(zenThis);" timeout="0"/>
</vgroup>
</pane>
]]></Data>
</XData>

<Method name="CheckConditions">
<Description><![CDATA[
Callback that is executed when creating the resultset for the main table.
Returns <query>MultiTypeNS</query> class query]]></Description>
<FormalSpec>*pSC:%Status,pInfo:%ZEN.Auxiliary.QueryInfo</FormalSpec>
<ReturnType>%ResultSet</ReturnType>
<Implementation><![CDATA[
	Do:..#CANCANCELSEARCH ..PrepareCancel()
	Set pSC = $$$OK
	Set tRS = ""

	Set tSearchCriteria = ..GetSearchCriteria()
	
	Set tDefaults = $P(tSearchCriteria,"^",3)
	Set tConfigName = $S(""'=..matchSource:..matchSource, 1:$P(tDefaults,",",1))
	Set tStartTime = $P(tDefaults,",",8)
	Set tEndTime = $P(tDefaults,",",9)
	
	Set tEventTypes = $P(tSearchCriteria,"^",4)
	Set tTypeCount = 0
	Set tEventNames = $lb("Assert","Error","Warning","Info","Trace","Alert")
	Set tTypes = ""
	For i=1:1:6 {
		Set tInclude = $P(tEventTypes,",",i)
		If $S(tInclude="true":1,tInclude="false":0,1:tInclude) {
			Set tTypeCount = tTypeCount + 1 
			Set tTypes = tTypes _ $listbuild($listget(tEventNames,i))
		}
	}

	Set tRS = ##class(%Library.ResultSet).%New("EnsPortal.EventLogNS:MultiType")
	#; Get inclusive StartID and EndID values
	Set tStartID = $piece(tSearchCriteria,",",6)
	Set tEndID = $piece(tSearchCriteria,",",7)
	If (tStartID && tEndID) && (tEndID < tStartID) {
		Set tTempID = tStartID
		Set tStartID = tEndID
		Set tEndID = tTempID
	}
	Set tMinID = $select(tStartID: tStartID-1, 1: "")
	Set tMaxID = $select(tEndID: tEndID+1, 1: "")
	Do ..AdjustMinMax(.tMinID,.tMaxID)

	
	Set tConditions = ##class(%ZEN.proxyObject).%New()
	Set:tMinID tConditions.MinID = tMinID
	Set:tMaxID tConditions.MaxID = tMaxID
	// Pick up any other restrictions
	Set tConditions.ConfigName = tConfigName
	Set tConditions.SourceClass = $piece(tDefaults,",",2)
	Set tConditions.SourceMethod = $piece(tDefaults,",",3)
	Set tConditions.Text = $piece(tDefaults,",",4)
	Set tConditions.Job = $piece(tDefaults,",",5)
	Set tConditions.SessionID = $piece(tDefaults,",",10)
	Set tConditions.NamespaceList = $P(tSearchCriteria,"^",8)
	Set tConditions.StartTime = tStartTime
	Set tConditions.EndTime = tEndTime
	
	
	Set tDescending = $select($piece(tSearchCriteria,"^",1) = "Newest First": 1, 1: 0)
	Set pSC = tRS.Execute($listtostring(tTypes),tDescending,,tConditions)
	If $$$ISERR(pSC) Quit ""
	
	Set pInfo.queryText = "EnsPortal.EventLogNS:MultiType class query"
	Set pInfo.queryExecuted = 1
	Set tMaxRows = $P(tSearchCriteria,"^",5)
	If (tMaxRows = "") Set tMaxRows = 500
	Set pInfo.rows = tMaxRows
	Quit tRS
]]></Implementation>
</Method>

<Method name="SaveDefaults">
<Description>
Save user's last search criteria into global for page default</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>SortOrder,TimeFormat,Defaults,EventType,MaxRows,States,AutoRefresh,NamespaceList=$Namespace</FormalSpec>
<ZenMethod>1</ZenMethod>
<Implementation><![CDATA[
	For p = 1:1:$L(EventType,",") Set pv=$P(EventType,",",p),$P(EventType,",",p) = $S(pv="true":1,pv="false":0,1:pv)
	For p = 1:1:$L(States,",") Set pv=$P(States,",",p),$P(States,",",p) = $S(pv="true":1,pv="false":0,1:pv)
	Set AutoRefresh = $S(AutoRefresh="true":1,AutoRefresh="false":0,1:AutoRefresh)
	Do ..SetDefaultString(SortOrder_"^"_TimeFormat_"^"_Defaults_"^"_EventType_"^"_MaxRows_"^"_States_"^"_AutoRefresh _"^"_ NamespaceList)
]]></Implementation>
</Method>

<Method name="GetSearchCriteria">
<CodeMode>expression</CodeMode>
<Private>1</Private>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[$get(^CacheTemp.EnsPortal($Username,"EventLogNS","SearchCriteria"),"Newest First^999^^1,1,1,1,1,1^500^0,0,0,0,0^1^"_$Namespace)
]]></Implementation>
</Method>

<Method name="SetDefaultString">
<ClassMethod>1</ClassMethod>
<FormalSpec>pDefaults:%String=""</FormalSpec>
<Private>1</Private>
<Implementation><![CDATA[
	If pDefaults = "" {
		Kill ^CacheTemp.EnsPortal($username,"EventLogNS","SearchCriteria")
	}
	Else {
		Set ^CacheTemp.EnsPortal($username,"EventLogNS","SearchCriteria") = pDefaults
	}
]]></Implementation>
</Method>

<Method name="GetAndUseDefaults">
<Description><![CDATA[
Method called from <method>%OnAfterCreatePage</method> to get defaults and build up the initial display]]></Description>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	do ##super()
	Set SearchCriteria = ..GetSearchCriteria()
	Set NamespceList = $P(SearchCriteria,"^",8)
]]></Implementation>
</Method>

<Query name="MultiType">
<Type>%Query</Type>
<FormalSpec>pTypes:%String="",pDescending:%Boolean=0,pSelectMode:%Integer=2,pConditions:%ZEN.proxyObject=""</FormalSpec>
<Parameter name="CONTAINID" value="2"/>
<Parameter name="ROWSPEC" value="Type:Ens.DataType.LogType,ID:%String,TimeLogged:%String,SessionId:%String,Job:%String,ConfigName:%String,Text:%String,TraceCat:%String,SourceClass:%String,SourceMethod:%String,TimeLogged999:%String,TimeLogged12:%String,Namespace:%String"/>
</Query>

<Method name="ConstructWhereCause">
<Description>
Construct WHERE part of an SQL statement. </Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&qHandle:%Binary,pTypes,pConditions:%ZEN.proxyObject=""]]></FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	Set tWhereConditions = ..ConstructWhereConditions(.qHandle, pTypes, pConditions) 
	If (tWhereConditions'=""){
		Set tWhereCause = " WHERE " _ tWhereConditions
	} Else {
		Set tWhereCause = ""
	}
	Quit tWhereCause
]]></Implementation>
</Method>

<Method name="ConstructWhereConditions">
<Description><![CDATA[
Construct WHERE <Condition(s)> part of an SQL statement. Returns empty string if there is no conditions.]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&qHandle:%Binary,pTypes,pConditions:%ZEN.proxyObject=""]]></FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	Set qHandle("mainArgs") = 0
	Set tSpecialConditions = ""
	If $IsObject(pConditions) {
		If (pConditions.MinID '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("ID > ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.MinID
		}
		If (pConditions.MaxID '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("ID < ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.MaxID
		}
		If (pConditions.Job '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("Job = ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.Job
		}
		If (pConditions.SessionID '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("SessionId = ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.SessionID
		}
		If (pConditions.SourceClass '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("SourceClass LIKE ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.SourceClass
		}
		If (pConditions.SourceMethod '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("SourceMethod LIKE ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.SourceMethod
		}
		If (pConditions.Text '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("Text [ ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.Text
		}
		If (pConditions.StartTime '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("TimeLogged >= ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.StartTime
		}
		If (pConditions.EndTime '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("TimeLogged <= ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.EndTime
		}
		If (pConditions.ConfigName '= "") {
			Set tSpecialConditions = tSpecialConditions _ $LB("ConfigName LIKE ?")
			Set qHandle("mainArgs") = qHandle("mainArgs") + 1
			Set qHandle("mainArgs",qHandle("mainArgs")) = pConditions.ConfigName
		}
	}
		
	Set tTypeCount = $length(pTypes,",")
	If ((pTypes'="") && (tTypeCount>0)) {
		Set tTypesSQL=""
		For i=1:1:tTypeCount {
			Set tType = $piece(pTypes,",",i)
			If (tType '= "") && '##class(Ens.DataType.LogType).IsValid(tType) {
				Set tTypesSQL =tTypesSQL _ $lb("'" _ tType _ "'")
			}
			
		}
		Set tTypesSQL = $LTS(tTypesSQL,", ")
		Set tSpecialConditions = tSpecialConditions _ $LB("Type IN (" _ tTypesSQL _ ")")
	}
	Quit $LTS(tSpecialConditions," AND ")
]]></Implementation>
</Method>

<Method name="ConstructOrgerByCause">
<Description>
Construct ORDER BY part of an SQL statement. </Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>pDescending:%Boolean=1</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	Set tOrder = $select(pDescending: "DESC", 1: "ASC")
	Quit " ORDER BY ID " _ tOrder
]]></Implementation>
</Method>

<Method name="MultiTypeExecute">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&qHandle:%Binary,pTypes:%String="",pDescending:%Boolean=1,pSelectMode:%Integer=2,pConditions:%ZEN.proxyObject=""]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set tStatus = $$$OK
	Try {
		If (($IsObject(pConditions)) && (pConditions.NamespaceList '= "")) {
			Set qHandle("NamespaceList") = pConditions.NamespaceList
			Set qHandle("OriginalNamespace") = $Namespace
			Set qHandle("NamespaceCount") = $L(qHandle("NamespaceList"),",")
			Set qHandle("CurrentNamespaceNumber") = 1
			Set qHandle("CurrentNamespace") = $P(qHandle("NamespaceList"),",",qHandle("CurrentNamespaceNumber"))		
		}
		
		Set tMainStatement = ##class(%SQL.Statement).%New()
		Set tMainStatement.%SelectMode = pSelectMode

		Set tSQL = ..#BaseSQL _  ..ConstructWhereCause(.qHandle, pTypes, pConditions) _ ..ConstructOrgerByCause(pDescending)
		Set qHandle("SQL") = tSQL
		Set tStatus = tMainStatement.%Prepare(tSQL)
		If $$$ISERR(tStatus) Quit
		Merge tMainArgs = qHandle("mainArgs")
		Set tRS = tMainStatement.%Execute(tMainArgs...)
		Set qHandle("mainStmt") = tMainStatement
		Set qHandle("tRS") = tRS // %sqlcq class extending %SQL.ISelectResult
	}
	Catch {
		Set tStatus = $$$EnsSystemError
	}
	Quit tStatus
]]></Implementation>
</Method>

<Method name="MultiTypeClose">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&qHandle:%Binary]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Zn qHandle("OriginalNamespace")
	Kill qHandle 
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="MultiTypeFetch">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&qHandle:%Binary,&Row:%List,&AtEnd:%Integer=0]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	Set tStatus = $$$OK
	Try {
		Set Row = ""
		While (Row = "") {
			If qHandle("tRS").%Next() { // We are iterating over resultset
				Set tDataRS = qHandle("tRS")
				Set Row = $lb(tDataRS.Type,tDataRS.ID,tDataRS.TimeLogged,tDataRS.SessionId,tDataRS.Job,tDataRS.ConfigName,tDataRS.Text,tDataRS.TraceCat,tDataRS.SourceClass,tDataRS.SourceMethod,tDataRS.TimeLogged,$piece(tDataRS.TimeLogged," ",2),tDataRS.Namespace)
			} Else { // We have reached the end of current resultset
				If qHandle("CurrentNamespaceNumber") < qHandle("NamespaceCount") { // Check, if there are yet namespaces left to execute our query in
					Set qHandle("CurrentNamespaceNumber") = qHandle("CurrentNamespaceNumber") + 1
					Set qHandle("CurrentNamespace") = $P(qHandle("NamespaceList"),",",qHandle("CurrentNamespaceNumber"))
					Zn qHandle("CurrentNamespace")
					Set tStatus = qHandle("mainStmt").%Prepare(qHandle("SQL"))
					If $$$ISERR(tStatus) Quit
					Merge tMainArgs = qHandle("mainArgs")
					Set tRS = qHandle("mainStmt").%Execute(tMainArgs...)
					Set qHandle("tRS") = tRS
				} Else { //  We iterated over all required namespaces
					Set AtEnd = 1
					Quit
				}
			}
		}
			
	}
	Catch {
		Set tStatus = $$$EnsSystemError
	}
	Quit tStatus
]]></Implementation>
</Method>

<Method name="GetEnsembleNamespaces">
<Description>
Returns info about availible Ensemble-enabled Namespaces</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	Set tStatus = ##class(%CSP.Portal.Utils).%GetNamespaceList(.tNSList)
	If $$$ISERR(tStatus) Quit tStatus
	Set tNamespace = $O(tNSList(""))
	Set tEnsNSList = ""
	While tNamespace '= "" {
		Set tEnabled = $Lg(tNSList(tNamespace),1)
		If tEnabled && ##class(%EnsembleMgr).IsEnsembleNamespace() {
			Set tEnsNSList = tEnsNSList _ $lb(tNamespace)
		}
		Set tNamespace = $O(tNSList(tNamespace))
	}
	return tEnsNSList
]]></Implementation>
</Method>

<Method name="AdjustMinMax">
<Description>
Ensure that we check the table's sql to see whether there is any paging based on a condition on "ID"
This is OK (for now) because the main StartID and EndID restrictions use conditions on "%ID"</Description>
<FormalSpec><![CDATA[&pMinID:%Integer,&pMaxID:%Integer]]></FormalSpec>
<Implementation><![CDATA[
	Set tTable = ..%GetComponentById("table")
	If $IsObject(tTable) && (tTable.sql '= "") {
		Set tModifiers = $piece(tTable.sql," FROM Ens_Util.Log",2)
		If (tModifiers '= "") && (tModifiers [ " WHERE ") {
			Set tWhere = $piece($piece(tModifiers," ORDER BY ID",1),"WHERE ",2)
			If (tWhere '= "") {
				For i=1:1:$length(tWhere," AND ") {
					#; remove trailing whitespace so the value is at end
					Set tCondition = $zstrip($piece(tWhere," AND ",i),">W")
					Set tProp = $piece(tCondition," ",1)
					If (tProp = "ID") {
						Set tOp = $piece($piece(tCondition,tProp_" ",2)," ",1)
						Set tVal = $piece(tCondition," ",*)
						If (tOp = "<") {
							Set pMaxID = $select(tVal = "": pMaxID, pMaxID = "": tVal, pMaxID < tVal: pMaxID, 1: tVal)
						}
						ElseIf (tOp = ">") {
							Set pMinID = $select(tVal = "": pMinID, pMinID = "": tVal, pMinID > tVal: pMinID, 1: tVal)
						}
					}
				}
			}
		}
	}
]]></Implementation>
</Method>

<Method name="onSearchHandler">
<Description>
User clicked the Search button, user changed a Quick Search or Event Type criterion, 
or we have completed a non-zero Purge.</Description>
<Language>javascript</Language>
<ClientMethod>1</ClientMethod>
<Implementation><![CDATA[
	zen("pageTotal").setValue(1);

	var table = this.getResultsTable();
	var where = "";

	var SortOrder = zen("SortOrder").getValue();

	// modify Select TimeExecuted based on Time Format
	var TimeFormat = zen('TimeFormat').getValue();
	zen('idTimeLogged12').setProperty('hidden',TimeFormat == '999');
	zen('idTimeLogged999').setProperty('hidden',TimeFormat == '12');

	// whereClause, orderByClause, and groupByClause	
	var ConfigName = zen("ConfigName").value;
	var SourceClass = zen("SourceClass").value;
	var SourceMethod = zen("SourceMethod").value;
	var Text = zen("Text").value;
	var Job = zen("Job").value;

	var StartId = zen("StartId").value;
	var EndId = zen("EndId").value;
	var StartTime = zen("StartTime").value;
	var EndTime = zen("EndTime").value;
	var SessionId = zen("SessionId").value;

	var Assert = zen("Assert").value;
	var Error = zen("Error").value;
	var Warning = zen("Warning").value;
	var Info = zen("Info").value;
	var Trace = zen("Trace").value;
	var Alert = zen("Alert").value;
	
	var Defaults = ConfigName+","+SourceClass+","+SourceMethod+","+Text+","+Job+","+StartId+","+EndId+","+StartTime+","+EndTime+","+SessionId;
	var EventType = Assert+","+Error+","+Warning+","+Info+","+Trace+","+Alert;
	
	// call server method to build the where clause
	var where = zenPage.GetWhereClause(Defaults,EventType);
	table.whereClause = where;
	table.orderByClause = "%ID";
	
	if (SortOrder == "Newest First") table.orderByClause = "%ID desc";
	
	rows = zen('MaxRows');
	var MaxRows = parseInt(rows.value);
	if (MaxRows > 10000) MaxRows = 100;
	if (MaxRows < 0) MaxRows = 1;
	
	// Get the current expando state and save it
	var States = zenPage.expandoState();
	
	// Get the Auto-Refresh choice and save it
	var auto = zen("chkRefresh");
	var AutoRefresh = auto.getValue();
	
	// Save last selections to global as default for page
	zenPage.SaveDefaults(SortOrder,TimeFormat,Defaults,EventType,MaxRows,States,AutoRefresh,"ENSLOGVIEWER,ENSDEMO");
]]></Implementation>
</Method>
</Class>
</Export>
